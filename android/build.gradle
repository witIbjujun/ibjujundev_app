// 빌드 kotlin 선언
buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'com.google.gms:google-services:4.3.15' // Firebase Gradle Plugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    afterEvaluate { project ->
        if (project.extensions.findByName("android") != null) {

            //#. 현재 플러그인의 compileSdk 버전을 가져옴
            Integer pluginCompileSdk = project.android.compileSdk

            //#. compileSdk 버전이 null이 아니고, 31보다 낮으면 강제로 31로 변경
            if (pluginCompileSdk != null && pluginCompileSdk < 31) {

                //#. 그리고 compileSdk버전이 변경되었다고 경고 로그
                project.logger.error(
                        "Warning: Overriding compileSdk version in Flutter plugin: "
                                + project.name
                                + " from "
                                + pluginCompileSdk
                                + " to 31 (to work around https://issuetracker.google.com/issues/199180389)."
                                + "\nIf there is not a new version of " + project.name + ", consider filing an issue against "
                                + project.name + " to increase their compileSdk to the latest (otherwise try updating to the latest version)."
                )
                //#. compileSdk 버전을 31로 강제로 설정
                project.android {
                    compileSdk 31
                }
            }
        }
    }
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}